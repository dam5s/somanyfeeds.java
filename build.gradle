group = "com.somanyfeeds"
version = "1.0-SNAPSHOT"
description = """Somanyfeeds -  A simple feed aggregator"""

ext {
    springVersion = "4.1.1.RELEASE"
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

apply plugin: "java"
apply plugin: "war"
apply plugin: "jetty"

sourceCompatibility = 1.8
targetCompatibility = 1.8

task("deploy", dependsOn: build) << {
    def warFile = "build/libs/somanyfeeds-1.0-SNAPSHOT.war"
    def buildpack = "https://github.com/spring-io/java-buildpack"
    def appName = "itsdamo"

    println "running: cf push $appName -p $warFile -b $buildpack"

    def builder = new ProcessBuilder("cf", "push", appName, "-p", warFile, "-b", buildpack)
    builder.inheritIO()

    def proc = builder.start()
    proc.waitFor()
}

war {
    configurations {
        providedRuntime
    }
}

task("dartDependencies", type: Exec) {
    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.INFO

    inputs.file "src/main/resources/dart/pubspec.yaml"
    outputs.dir "src/main/resources/dart/packages"

    workingDir "src/main/resources/dart"
    commandLine "pub", "get"
}

task("buildDart", type: Exec, dependsOn: "dartDependencies") {
    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.INFO

    inputs.file "src/main/resources/dart/application.dart"
    outputs.dir "src/main/resources/dart/application.dart.js"

    workingDir "src/main/resources/dart"
    commandLine "dart2js", "-m", "-o", "application.dart.js", "application.dart"
}

war.dependsOn "buildDart"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Spring
    compile "org.springframework:spring-webmvc:$springVersion"
    compile "javax.servlet:javax.servlet-api:3.0.1"

    // JSP
    compile "jstl:jstl:1.2"

    // HTTP
    compile "com.google.http-client:google-http-client:1.18.0-rc"

    // XML Parsing
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.4.0"
    compile "org.codehaus.woodstox:woodstox-core-asl:4.1.4"

    // Logging
    compile "org.slf4j:slf4j-api:1.7.7"

    // Testing
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile "junit:junit:4.11"
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "org.mockito:mockito-core:1.9.5"
}
