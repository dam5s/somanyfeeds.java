group = "com.somanyfeeds"
version = "1.0-SNAPSHOT"
description = """Somanyfeeds -  A simple feed aggregator"""

ext {
    springVersion = "4.0.6.RELEASE"
    springBootVersion = "1.1.4.RELEASE"
    groovyVersion = "2.3.3"
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.4.RELEASE")
    }
}

apply plugin: "java"
apply plugin: "spring-boot"
apply plugin: "war"

sourceCompatibility = 1.8
targetCompatibility = 1.8

task(deploy, dependsOn: build) << {
    def warFile = "build/libs/somanyfeeds-1.0-SNAPSHOT.war"
    def buildpack = "https://github.com/spring-io/java-buildpack"

    println "running: cf push itsdamo -p $warFile -b $buildpack"

    def builder = new ProcessBuilder("cf", "push", "itsdamo", "-p", warFile, "-b", buildpack)
    builder.inheritIO()

    def proc = builder.start()
    proc.waitFor()
}

war {
    configurations {
        providedRuntime
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Spring Boot
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"

    // Groovy Templates
    compile "org.codehaus.groovy:groovy:$groovyVersion"
    compile "org.codehaus.groovy:groovy-templates:$groovyVersion"

    // HTTP
    compile "com.google.http-client:google-http-client:1.18.0-rc"

    // XML Parsing
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.4.0"
    compile "org.codehaus.woodstox:woodstox-core-asl:4.1.4"

    // Logging
    compile "org.slf4j:slf4j-api:1.7.7"

    // Testing
    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-core:1.9.5"
}
